#!/usr/bin/env python

from os.path import join

Import('ran_scripts')
if 'src/SConscript' in ran_scripts: Return()
ran_scripts.append('src/SConscript')

### Setup environ ###
Import('env', 'conf')
env = env.Clone()
bld_dir = join('..','build')

# Call subsidary SConscripts
base_installers, base_objects = SConscript('base/SConscript')
streaming_installers, streaming_objects = SConscript('streaming/SConscript')
algo_objects = SConscript('algorithms/SConscript')

# All objects for library
objs = (base_objects, streaming_objects, algo_objects)

# Create essentia library
lib_type = ARGUMENTS.get('library', 'static')

if lib_type == 'static':
    essentia_lib = env.StaticLibrary(join(bld_dir,'essentia'), objs)

elif lib_type == 'dynamic':
    essentia_lib = env.SharedLibrary(join(bld_dir,'essentia'), objs)

else:
    print('\'library\' argument not of the set (\'static\', \'dynamic\'): '+lib_type)


# Create aliases
env.Alias('build', essentia_lib)
env.Alias('install', env.Install( join(conf.DESTDIR, conf.PREFIX, 'lib'), essentia_lib))
env.Alias('install', [base_installers, streaming_installers])
